package org.compi2.codigo3d.parser;

import java_cup.runtime.Symbol;

//PARSER PARA EL ARCHIVO DE ESCENARIOS
parser code
{://Codigo visible

    public void syntax_error(Symbol s){
        System.err.println(s);
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.err.println("T.T");
        System.err.println(s);
    }

:}

action code
{://Codigo de acciones
      
:}
// Seccion de terminales
terminal String ID, ENTERO, DECIMAL;
terminal TokenC3D MAS, MENOS, MULT, DIV, POT, PAR_IZQ, PAR_DER;
terminal TokenC3D IGUAL, DIFERENTE,MENORQ, MAYORQ, MAYORIGUALQ, MENORIGUALQ;
terminal String AND, OR, NOT;

// Seccion de no terminales 
nonterminal NodoC3D inicial, expresion, oprel;
nonterminal NodoC3DRel relacion, condicion, A, O, D;

precedence left OR;
precedence left AND;
precedence right NOT;
precedence left MAS, MENOS;
precedence left MULT, DIV; 
precedence right POT; 

start with inicial;

inicial     ::= {: ControlC3D.reiniciar(); :} condicion:cond 
                    {: 
                        RESULT = cond;
                       // ControlC3D.agregarC3D("//Ãšltimo valor: " + cond.getCad());
                        ControlC3D.agregarC3D(                      cond.getCod());

                        ControlC3D.agregarC3D( cond.getResultadoFinal());
                    :};

condicion   ::= condicion:izq OR:or condicion:der
                    {: 
                       // String etq = ControlC3D.generaETQ();
                        String c3d =  izq.getCod() +  izq.getEtiquetaFalsa()+ ": \n  "   + der.getCod()  ;
                        NodoC3DRel nodo = new NodoC3DRel("COND_AND");
                        nodo.setEtiquetaVerdadera(izq.getEtiquetaVerdadera()+","+ der.getEtiquetaVerdadera() );
                        nodo.setEtiquetaFalsa(der.getEtiquetaFalsa());
                       // ControlC3D.agregarC3D(c3d);
                        nodo.setCod(c3d);
                        RESULT = nodo;// new NodoC3D(temp); 
                    :} 
           | condicion:izq AND:and condicion:der
                    {: 
                       // String etq = ControlC3D.generaETQ();
                        String c3d =  izq.getCod() +  izq.getEtiquetaVerdadera()+ ": \n  "   + der.getCod()  ;
                        NodoC3DRel nodo = new NodoC3DRel("COND_AND");
                        nodo.setEtiquetaVerdadera(der.getEtiquetaVerdadera());
                        nodo.setEtiquetaFalsa(izq.getEtiquetaFalsa()+","+der.getEtiquetaFalsa());
                       // ControlC3D.agregarC3D(c3d);
                        nodo.setCod(c3d);
                        RESULT = nodo;// new NodoC3D(temp); 
                    :} 

            | NOT:not condicion:cond
                    {: 
                       // String temp = ControlC3D.generaTemp();
                     //   String c3d = temp + " = "  + not + cond.getCad() + ";\n";
                       // ControlC3D.agregarC3D(c3d);
                        NodoC3DRel nodo = new NodoC3DRel("COND_NOT");
                        nodo.setEtiquetaVerdadera(cond.getEtiquetaFalsa());
                        nodo.setEtiquetaFalsa(cond.getEtiquetaVerdadera());
                        nodo.setCod(cond.getCod());
                        RESULT = nodo;//new NodoC3D(temp); 
                    :}
            | relacion:rel
                    {:
                        RESULT = rel; 
                     :}
;



relacion   ::= expresion:izq oprel:op expresion:der
                    {: 
                        NodoC3DRel nodo = new NodoC3DRel("REL");
                        nodo.setEtiquetaVerdadera(ControlC3D.generaETQ());
                        nodo.setEtiquetaFalsa(ControlC3D.generaETQ());
                        // String c3d = temp + " = " + izq.getCad() + op.getCad() + der.getCad() + ";\n";
                         String c3d = izq.getCod() + der.getCod() + " if ( " + izq.getCad() +" " +op.getCad() +" "+ der.getCad() + ") goto " + nodo.getEtiquetaVerdadera()+ " ;\n  goto "+ nodo.getEtiquetaFalsa() +" ;\n" ;
                       // ControlC3D.agregarC3D(c3d); 
                        nodo.setCod(c3d);
                        RESULT = nodo; // new NodoC3D(temp); 
                    :}
;

oprel   ::=  IGUAL            
                    {: RESULT = new NodoC3D("=="); :}
            |   DIFERENTE  
                    {: RESULT = new NodoC3D("!="); :}
            |   MENORQ 
                    {: RESULT = new NodoC3D("<"); :}
            |   MAYORQ
                    {: RESULT = new NodoC3D(">"); :}
            |   MENORIGUALQ
                    {: RESULT = new NodoC3D("<="); :}
            |   MAYORIGUALQ  
                    {: RESULT = new NodoC3D(">="); :}
            ;

expresion   ::= expresion:izq MAS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d =izq.getCod() + der.getCod() + temp + " = " + izq.getCad() + " + " + der.getCad() + ";\n";
                    // ControlC3D.agregarC3D(c3d);
                        NodoC3D nodo = new NodoC3D(temp); 
                        nodo.setCod(c3d);
                        RESULT = nodo;
                    :}
            |   expresion:izq MENOS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " - " + der.getCad() + ";\n";
                    // ControlC3D.agregarC3D(c3d);
                        NodoC3D nodo = new NodoC3D(temp); 
                        nodo.setCod(c3d);
                        RESULT = nodo;
                    :}
            |   expresion:izq MULT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = izq.getCod() + der.getCod() + temp + " = " + izq.getCad() + " * " + der.getCad() + ";\n";
                    // ControlC3D.agregarC3D(c3d);
                        NodoC3D nodo = new NodoC3D(temp); 
                        nodo.setCod(c3d);
                        RESULT = nodo;
                    :}
            |   expresion:izq DIV expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = izq.getCod() + der.getCod() + temp + " = " + izq.getCad() + " / " + der.getCad() + ";\n";
                    // ControlC3D.agregarC3D(c3d);
                        NodoC3D nodo = new NodoC3D(temp); 
                        nodo.setCod(c3d);
                        RESULT = nodo;
                    :}
            |   expresion:izq POT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = izq.getCod() + der.getCod() + temp + " = " + izq.getCad() + " ^ " + der.getCad() + ";\n";
                       // ControlC3D.agregarC3D(c3d);
                        NodoC3D nodo = new NodoC3D(temp); 
                        nodo.setCod(c3d);
                        RESULT = nodo;
                    :}
            |   MENOS expresion:exp 
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d  = exp.getCod() ;
                         c3d +=  temp + " = - " + exp.getCad() + ";\n";
                       // ControlC3D.agregarC3D(c3d);
                        NodoC3D nodo = new NodoC3D(temp); 
                        nodo.setCod(c3d);
                        RESULT = nodo;
                    :}
            |   PAR_IZQ expresion:exp PAR_DER   
                    {: RESULT = exp; :}
            |   ID:id           
                    {: RESULT = new NodoC3D(id); :}
            |   ENTERO:entero   
                    {: RESULT = new NodoC3D(entero); :}
            |   DECIMAL:decimal 
                    {: RESULT = new NodoC3D(decimal); :}
            ;